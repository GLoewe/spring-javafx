plugins {
    id "com.jfrog.bintray" version "1.4"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    group = 'org.springframework'
    version = '1.0.0-SNAPSHOT'

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        publications = ['maven']
        pkg {
            repo = 'generic'
            name = name
            desc = 'Spring JavaFX Integration'
            labels = ['spring', 'javafx']
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/schulzp/spring-javafx.git'
            websiteUrl = 'https://github.com/schulzp/spring-javafx'
            issueTrackerUrl = 'https://github.com/schulzp/spring-javafx/issues'
            publicDownloadNumbers = true
        }
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        vSpringCore = '4.2.3.RELEASE'
        vSpringBoot = '1.3.0.RELEASE'
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }
}