plugins {
    id 'com.jfrog.bintray' version '1.4'
    id 'net.researchgate.release' version '2.3.4'
}

release {
    buildTasks = ['releaseBuild']
}

task releaseBuild {
    dependsOn(
            'spring-javafx:build',
            'spring-javafx:bintrayUpload',
            'spring-boot-javafx:build',
            'spring-boot-javafx:bintrayUpload'
    )
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'

    group = 'org.springframework'

    release {
        failOnPublishNeeded = false
        failOnSnapshotDependencies = false
    }

}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }

    ext {
        bintrayPackageVersion = version
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
        publications = ['maven']
        publish = true
        dryRun = false
        pkg {
            repo = 'maven'
            name = 'spring-javafx'
            desc = 'Spring JavaFX Integration'
            labels = ['spring', 'javafx']
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/schulzp/spring-javafx.git'
            websiteUrl = 'https://github.com/schulzp/spring-javafx'
            issueTrackerUrl = 'https://github.com/schulzp/spring-javafx/issues'
            publicDownloadNumbers = true
            version {
                name = bintrayPackageVersion
                desc = "Spring JavaFX Integration $bintrayPackageVersion"
                vcsTag = bintrayPackageVersion
            }
        }
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }
}